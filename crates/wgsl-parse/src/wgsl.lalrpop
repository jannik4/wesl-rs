use std::str::FromStr;
use std::path::PathBuf;
use crate::{error::CustomLalrError, lexer::Token};
use crate::span::{Spanned, Span};
use crate::syntax::*;
use crate::parser_support::*;

// this grammar follows closely the wgsl spec.
// follwing the spec at this date: https://www.w3.org/TR/2024/WD-WGSL-20240731/

grammar;

extern {
    type Location = usize;
    type Error = (usize, CustomLalrError, usize);
    enum Token {
        // syntactic tokens
        // https://www.w3.org/TR/WGSL/#syntactic-tokens
        "&" => Token::SymAnd,
        "&&" => Token::SymAndAnd,
        "->" => Token::SymArrow,
        "@" => Token::SymAttr,
        "/" => Token::SymForwardSlash,
        "!" => Token::SymBang,
        "[" => Token::SymBracketLeft,
        "]" => Token::SymBracketRight,
        "{" => Token::SymBraceLeft,
        "}" => Token::SymBraceRight,
        ":" => Token::SymColon,
        "," => Token::SymComma,
        "=" => Token::SymEqual,
        "==" => Token::SymEqualEqual,
        "!=" => Token::SymNotEqual,
        ">" => Token::SymGreaterThan,
        ">=" => Token::SymGreaterThanEqual,
        ">>" => Token::SymShiftRight,
        "<" => Token::SymLessThan,
        "<=" => Token::SymLessThanEqual,
        "<<" => Token::SymShiftLeft,
        "%" => Token::SymModulo,
        "-" => Token::SymMinus,
        "--" => Token::SymMinusMinus,
        "." => Token::SymPeriod,
        "+" => Token::SymPlus,
        "++" => Token::SymPlusPlus,
        "|" => Token::SymOr,
        "||" => Token::SymOrOr,
        "(" => Token::SymParenLeft,
        ")" => Token::SymParenRight,
        ";" => Token::SymSemicolon,
        "*" => Token::SymStar,
        "~" => Token::SymTilde,
        "_" => Token::SymUnderscore,
        "^" => Token::SymXor,
        "+=" => Token::SymPlusEqual,
        "-=" => Token::SymMinusEqual,
        "*=" => Token::SymTimesEqual,
        "/=" => Token::SymDivisionEqual,
        "%=" => Token::SymModuloEqual,
        "&=" => Token::SymAndEqual,
        "|=" => Token::SymOrEqual,
        "^=" => Token::SymXorEqual,
        ">>=" => Token::SymShiftRightAssign,
        "<<=" => Token::SymShiftLeftAssign,

        // keywords
        // https://www.w3.org/TR/WGSL/#keyword-summary
        "alias" => Token::KwAlias,
        "break" => Token::KwBreak,
        "case" => Token::KwCase,
        "const" => Token::KwConst,
        "const_assert" => Token::KwConstAssert,
        "continue" => Token::KwContinue,
        "continuing" => Token::KwContinuing,
        "default" => Token::KwDefault,
        "diagnostic" => Token::KwDiagnostic,
        "discard" => Token::KwDiscard,
        "else" => Token::KwElse,
        "enable" => Token::KwEnable,
        "false" => Token::KwFalse,
        "fn" => Token::KwFn,
        "for" => Token::KwFor,
        "if" => Token::KwIf,
        "let" => Token::KwLet,
        "loop" => Token::KwLoop,
        "override" => Token::KwOverride,
        "requires" => Token::KwRequires,
        "return" => Token::KwReturn,
        "struct" => Token::KwStruct,
        "switch" => Token::KwSwitch,
        "true" => Token::KwTrue,
        "var" => Token::KwVar,
        "while" => Token::KwWhile,

        IdentPatternToken => Token::Ident(<String>),
        TokAbstractInt => Token::AbstractInt(<i64>),
        TokAbstractFloat => Token::AbstractFloat(<f64>),
        TokI32 => Token::I32(<i32>),
        TokU32 => Token::U32(<u32>),
        TokF32 => Token::F32(<f32>),
        TokF16 => Token::F16(<f32>),
        TokTemplateArgsStart => Token::TemplateArgsStart,
        TokTemplateArgsEnd => Token::TemplateArgsEnd,

        // extension: wesl-imports
        // https://github.com/wgsl-tooling-wg/wesl-spec/blob/imports-update/Imports.md
        // date: 2025-01-18, hash: 2db8e7f681087db6bdcd4a254963deb5c0159775
        #[cfg(feature = "imports")]
        "::" => Token::SymColonColon,
        #[cfg(feature = "imports")]
        "self" => Token::KwSelf,
        #[cfg(feature = "imports")]
        "super" => Token::KwSuper,
        #[cfg(feature = "imports")]
        "crate" => Token::KwCrate,
        #[cfg(feature = "imports")]
        "as" => Token::KwAs,
        #[cfg(feature = "imports")]
        "import" => Token::KwImport,
    }
}

// these are context-dependent names. e.g. the @const attribute.
#[cfg(not(feature = "imports"))]
Keyword: String = {
    "alias" => <>.to_string(),
    "break" => <>.to_string(),
    "case" => <>.to_string(),
    "const" => <>.to_string(),
    "const_assert" => <>.to_string(),
    "continue" => <>.to_string(),
    "continuing" => <>.to_string(),
    "default" => <>.to_string(),
    "diagnostic" => <>.to_string(),
    "discard" => <>.to_string(),
    "else" => <>.to_string(),
    "enable" => <>.to_string(),
    "false" => <>.to_string(),
    "fn" => <>.to_string(),
    "for" => <>.to_string(),
    "if" => <>.to_string(),
    "let" => <>.to_string(),
    "loop" => <>.to_string(),
    "override" => <>.to_string(),
    "requires" => <>.to_string(),
    "return" => <>.to_string(),
    "struct" => <>.to_string(),
    "switch" => <>.to_string(),
    "true" => <>.to_string(),
    "var" => <>.to_string(),
    "while" => <>.to_string(),
};

// the grammar rules are layed out in the same order as in the spec.
// follwing the spec at this date: https://www.w3.org/TR/2024/WD-WGSL-20240731/

// custom entrypoint called by the lexer when it sees [Token::Ident, Token::SymLessThan].
// if this parse succeeds, the next token emitted by the lexer will be TokTemplateList.
// pub TryTemplateList: Span = {
//    <l:@L> TokTemplateArgsStart TemplateArgCommaList TokTemplateArgsEnd <r:@R> => { (l..r) },
//};

// =====================
// === Begin grammar ===
// =====================

// 2. WGSL MODULE
// https://www.w3.org/TR/WGSL/#wgsl-module

#[cfg(not(feature = "imports"))]
pub TranslationUnit: TranslationUnit = {
    <global_directives: GlobalDirective*> <global_declarations: GlobalDecl*> => TranslationUnit {
        global_directives, global_declarations
    },
};

pub GlobalDecl: GlobalDeclaration = {
    ";"                        => GlobalDeclaration::Void,
    <GlobalVariableDecl> ";"   => GlobalDeclaration::Declaration(<>),
    <GlobalValueDecl> ";"      => GlobalDeclaration::Declaration(<>),
    <TypeAliasDecl> ";"        => GlobalDeclaration::TypeAlias(<>),
    <StructDecl>               => GlobalDeclaration::Struct(<>),
    <FunctionDecl>             => GlobalDeclaration::Function(<>),
    <ConstAssertStatement> ";" => GlobalDeclaration::ConstAssert(<>),
};

DiagnosticRuleName: String = {
    DiagnosticNameToken,
    <first: DiagnosticNameToken> "." <last: DiagnosticNameToken> => format!("{first}.{last}"),
};

// 3. TEXTUAL STRUCTURE
// https://www.w3.org/TR/WGSL/textual-structure#

// XXX: non-conformant
// https://www.w3.org/TR/WGSL/#syntax-literal
pub Literal: LiteralExpression = {
    TokAbstractInt   => LiteralExpression::AbstractInt(<>),
    TokAbstractFloat => LiteralExpression::AbstractFloat(<>),
    TokI32           => LiteralExpression::I32(<>),
    TokU32           => LiteralExpression::U32(<>),
    TokF32           => LiteralExpression::F32(<>),
    TokF16           => LiteralExpression::F16(<>),
    BoolLiteral,
};

BoolLiteral: LiteralExpression = {
    "true"  => LiteralExpression::Bool(true),
    "false" => LiteralExpression::Bool(false),
};

Ident: Ident = {
    IdentPatternToken /* _DisambiguateTemplate */ => {
        Ident::new(<>)
    },
};

MemberIdent: Ident = {
    IdentPatternToken => {
        Ident::new(<>)
    },
};

// TODO: check context-dependent names below.

BuiltinValueName: String = {
    IdentPatternToken => <>,
};

DiagnosticNameToken: String = {
    IdentPatternToken => <>,
};

SeverityControlName: DiagnosticSeverity = {
    <l: @L> <ident: IdentPatternToken> <r: @R> =>? {
        DiagnosticSeverity::from_str(&ident)
            .map_err(|()| lalrpop_util::ParseError::User{ error: (l, CustomLalrError::DiagnosticSeverity, r) })
    },
};

EnableExtensionName: String = {
    IdentPatternToken => <>,
};

SoftwareExtensionName: String = {
    IdentPatternToken => <>,
};

InterpolateSamplingName: String = {
    IdentPatternToken => <>,
};

SwizzleName: String = {
    IdentPatternToken => <>,
};

TemplateList: Vec<TemplateArg> = {
    TokTemplateArgsStart <TemplateArgCommaList> TokTemplateArgsEnd,
};

TemplateArgCommaList: Vec<TemplateArg> = {
    Comma1<TemplateArgExpression>,
};

TemplateArgExpression: TemplateArg = {
    ExpressionNode => TemplateArg { expression: <> },
};

// 4. DIRECTIVES
// https://www.w3.org/TR/WGSL/#directives

pub GlobalDirective: GlobalDirective = {
    DiagnosticDirective => GlobalDirective::Diagnostic(<>),
    EnableDirective     => GlobalDirective::Enable(<>),
    RequiresDirective   => GlobalDirective::Requires(<>),
};

#[cfg(not(feature = "attributes"))]
EnableDirective: EnableDirective = {
    "enable" <extensions: EnableExtensionList> ";" => EnableDirective {
        extensions
    },
};

EnableExtensionList: Vec<String> = {
    Comma1<EnableExtensionName>,
};

#[cfg(not(feature = "attributes"))]
RequiresDirective: RequiresDirective = {
    "requires" <extensions: SoftwareExtensionList> ";" => RequiresDirective {
        extensions
    },
};

SoftwareExtensionList: Vec<String> = {
    Comma1<SoftwareExtensionName>,
};

#[cfg(not(feature = "attributes"))]
DiagnosticDirective: DiagnosticDirective = {
    "diagnostic" <DiagnosticControl> ";" => {
        let (severity, rule_name) = <>;
        DiagnosticDirective { severity, rule_name }
    },
};

// 5. DECLARATION AND SCOPE
// https://www.w3.org/TR/WGSL/#declaration-and-scope

// 6. TYPES
// https://www.w3.org/TR/WGSL/#types

#[cfg(not(feature = "attributes"))]
StructDecl: Struct = {
    "struct" <ident: Ident> <members: StructBodyDecl> => Struct {
        ident, members
    },
};

StructBodyDecl: Vec<StructMember> = {
    "{" <Comma1<StructMember>> "}",
};

StructMember: StructMember = {
    <attributes: Attribute*> <ident: MemberIdent> ":" <ty: TypeSpecifier> => StructMember {
        attributes, ident, ty
    },
};

#[cfg(not(feature = "attributes"))]
TypeAliasDecl: TypeAlias = {
    "alias" <ident: Ident> "=" <ty: TypeSpecifier> => TypeAlias {
        ident, ty
    },
};

TypeSpecifier: TypeExpression = {
    TemplateElaboratedIdent,
};

TemplateElaboratedIdent: TypeExpression = <ident: Ident> <template_args: TemplateList?> => TypeExpression {
    ident, template_args
};

// 7. VARIABLE AND VALUE DECLARATIONS
// https://www.w3.org/TR/WGSL/#var-and-value

#[cfg(not(feature = "attributes"))]
VariableOrValueStatement: Declaration = {
    VariableDecl,
    <mut decl: VariableDecl> "=" <initializer: ExpressionNode> => {
        decl.initializer = Some(initializer);
        decl
    },
    "let" <id_ty: OptionallyTypedIdent> "=" <initializer: ExpressionNode> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes: Vec::new(),
            kind: DeclarationKind::Let,
            ident,
            ty,
            initializer: Some(initializer),
        }
    },
    "const" <id_ty: OptionallyTypedIdent> "=" <initializer: ExpressionNode> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes: Vec::new(),
            kind: DeclarationKind::Const,
            ident,
            ty,
            initializer: Some(initializer),
        }
    },
};

#[cfg(not(feature = "attributes"))]
VariableDecl: Declaration = {
    <l: @L> "var" <template_args: TemplateList?> <r: @R> <id_ty: OptionallyTypedIdent> =>? {
        let (ident, ty) = id_ty;
        let address_space = parse_var_template(template_args)
            .map_err(|e| lalrpop_util::ParseError::User{ error: (l, e, r) })?;
        Ok(Declaration {
            attributes: Vec::new(),
            kind: DeclarationKind::Var(address_space),
            ident,
            ty,
            initializer: None,
        })
    },
};

OptionallyTypedIdent: (Ident, Option<TypeExpression>) = {
    <Ident> <(":" <TypeSpecifier>)?>,
};

#[cfg(not(feature = "attributes"))]
GlobalVariableDecl: Declaration = {
    <attributes: Attribute*> <mut decl: VariableDecl> <initializer: ("=" <ExpressionNode>)?> => {
        decl.attributes = attributes;
        decl.initializer = initializer.map(Into::into);
        decl
    },
};

#[cfg(not(feature = "attributes"))]
GlobalValueDecl: Declaration = {
    "const" <id_ty: OptionallyTypedIdent> "=" <initializer: ExpressionNode> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes: Vec::new(),
            kind: DeclarationKind::Const,
            ident,
            ty,
            initializer: Some(initializer),
        }
    },
    <attributes: Attribute*> "override" <id_ty: OptionallyTypedIdent> <initializer: ("=" <ExpressionNode>)?> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes,
            kind: DeclarationKind::Override,
            ident,
            ty,
            initializer: initializer.map(Into::into),
        }
    },
};

// 8. EXPRESSIONS
// https://www.w3.org/TR/WGSL/#expressions

PrimaryExpression: Expression = {
    TemplateElaboratedIdent => Expression::TypeOrIdentifier(<>),
    CallExpression,
    Literal => Expression::Literal(<>),
    ParenExpression => Expression::Parenthesized(<>),
};

CallExpression: Expression = {
    CallPhrase => Expression::FunctionCall(<>),
};

CallPhrase: FunctionCall = {
    <ty: TemplateElaboratedIdent> <arguments: ArgumentExpressionList> => {
        FunctionCallExpression {
            ty, arguments
        }
    },
};

ParenExpression: ParenthesizedExpression = {
    "(" <Spanned<Expression>> ")" => ParenthesizedExpression {
        expression: <>
    },
};

ArgumentExpressionList: Vec<ExpressionNode> = {
    "(" <ExpressionCommaList?> ")" => <>.unwrap_or_default(),
};

ExpressionCommaList: Vec<ExpressionNode> = {
    Comma1<ExpressionNode>,
};

// was modified from the spec, but should be equivalent
ComponentOrSwizzleSpecifier: Vec<Spanned<Component>> = {
    // "[" Expression "]" ComponentOrSwizzleSpecifier?,
    // "." MemberIdent ComponentOrSwizzleSpecifier?,
    // "." SwizzleName ComponentOrSwizzleSpecifier ?,
    Spanned<SingleComponentOrSwizzleSpecifier>+
};

SingleComponentOrSwizzleSpecifier: Component = {
    "[" <Spanned<Expression>> "]" => Component::Index(<>),
    "." <MemberIdent>       => Component::Named(<>),
}

UnaryExpression: Expression = {
    SingularExpression,
    "-" <operand: Spanned<UnaryExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::Negation, operand
    }),
    "!" <operand: Spanned<UnaryExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::LogicalNegation, operand
    }),
    "~" <operand: Spanned<UnaryExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::BitwiseComplement, operand
    }),
    "*" <operand: Spanned<UnaryExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::Indirection, operand
    }),
    "&" <operand: Spanned<UnaryExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::AddressOf, operand
    }),
};

SingularExpression: Expression = {
    <expression: WithSpan<PrimaryExpression>> <components: ComponentOrSwizzleSpecifier?> => match components {
        Some(components) => apply_components(expression.0, expression.1, components),
        None => expression.0
    },
};

LhsExpression: Expression = {
    <expression: WithSpan<CoreLhsExpression>> <components: ComponentOrSwizzleSpecifier?> => match components {
        Some(components) => apply_components(expression.0, expression.1, components),
        None => expression.0
    },
    "*" <operand: Spanned<LhsExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::Indirection, operand
    }),
    "&" <operand: Spanned<LhsExpression>> => Expression::Unary(UnaryExpression {
        operator: UnaryOperator::AddressOf, operand
    }),
 };
 
CoreLhsExpression: Expression = {
    Ident /* DisambiguateTemplate */ => Expression::TypeOrIdentifier(<>.into()),
    "(" <Spanned<LhsExpression>> ")" => Expression::Parenthesized(ParenthesizedExpression {
        expression: <>
    }),
};

MultiplicativeExpression: Expression = {
    UnaryExpression,
    <left: Spanned<MultiplicativeExpression>> <operator: MultiplicativeOperator> <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator, left, right
    }),
};

MultiplicativeOperator: BinaryOperator = {
    "*" => BinaryOperator::Multiplication,
    "/" => BinaryOperator::Division,
    "%" => BinaryOperator::Remainder,
};
 
AdditiveExpression: Expression = {
    MultiplicativeExpression,
    <left: Spanned<AdditiveExpression>> <operator: AdditiveOperator> <right: Spanned<MultiplicativeExpression>> => Expression::Binary(BinaryExpression {
        operator, left, right
    }),
    // this handles the special case `x = 5--7` which must be parsed as `x = 5 - (-7)`
    <left: Spanned<AdditiveExpression>> <l: @L> "--" <right: Spanned<MultiplicativeExpression>> => {
        let outer_span = Span::new((l+1)..right.span.range().end);
        let right = Spanned::new(Expression::Unary(UnaryExpression {
            operator: UnaryOperator::Negation, operand: right,
        }), outer_span);
        Expression::Binary(BinaryExpression {
            operator: BinaryOperator::Subtraction, left, right
        })
    },
};

AdditiveOperator: BinaryOperator = {
    "+" => BinaryOperator::Addition,
    "-" => BinaryOperator::Subtraction,
};

ShiftExpression: Expression = {
    AdditiveExpression,
    <left: Spanned<UnaryExpression>> "<<" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::ShiftLeft, left, right
    }),
    <left: Spanned<UnaryExpression>> ">>" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::ShiftRight, left, right
    }),
};

RelationalExpression: Expression = {
    ShiftExpression,
    <left: Spanned<ShiftExpression>> "<" <right: Spanned<ShiftExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::LessThan, left, right
    }),
    <left: Spanned<ShiftExpression>> ">" <right: Spanned<ShiftExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::GreaterThan, left, right
    }),
    <left: Spanned<ShiftExpression>> "<=" <right: Spanned<ShiftExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::LessThanEqual, left, right
    }),
    <left: Spanned<ShiftExpression>> ">=" <right: Spanned<ShiftExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::GreaterThanEqual, left, right
    }),
    <left: Spanned<ShiftExpression>> "==" <right: Spanned<ShiftExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::Equality, left, right
    }),
    <left: Spanned<ShiftExpression>> "!=" <right: Spanned<ShiftExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::Inequality, left, right
    }),
};

ShortCircuitAndExpression: Expression = {
    RelationalExpression,
    <left: Spanned<ShortCircuitAndExpression>> "&&" <right: Spanned<RelationalExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::ShortCircuitAnd, left, right
    }),
 };
 
ShortCircuitOrExpression: Expression = {
    RelationalExpression,
    <left: Spanned<ShortCircuitOrExpression>> "||" <right: Spanned<RelationalExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::ShortCircuitOr, left, right
    }),
 };
 
BinaryOrExpression: Expression = {
    UnaryExpression,
    <left: Spanned<BinaryOrExpression>> "|" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::BitwiseOr, left, right
    }),
};

BinaryAndExpression: Expression = {
    UnaryExpression,
    <left: Spanned<BinaryAndExpression>> "&" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::BitwiseAnd, left, right
    }),
};

BinaryXorExpression: Expression = {
    UnaryExpression,
    <left: Spanned<BinaryXorExpression>> "^" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::BitwiseXor, left, right
    }),
};

BitwiseExpression: Expression = {
    <left: Spanned<BinaryAndExpression>> "&" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::BitwiseAnd, left, right
    }),
    <left: Spanned<BinaryOrExpression>> "|" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::BitwiseOr, left, right
    }),
    <left: Spanned<BinaryXorExpression>> "^" <right: Spanned<UnaryExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::BitwiseXor, left, right
    }),
};

pub Expression: Expression = {
    RelationalExpression,
    <left: Spanned<ShortCircuitOrExpression>> "||" <right: Spanned<RelationalExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::ShortCircuitOr, left, right
    }),
    <left: Spanned<ShortCircuitAndExpression>> "&&" <right: Spanned<RelationalExpression>> => Expression::Binary(BinaryExpression {
        operator: BinaryOperator::ShortCircuitAnd, left, right
    }),
    BitwiseExpression,
};

ExpressionNode: ExpressionNode = Spanned<Expression>;

// 9. STATEMENTS
// https://www.w3.org/TR/WGSL/#statements


CompoundStatement: CompoundStatement = {
    <attributes: Attribute*> "{" <statements: StatementNode*> "}" => CompoundStatement {
        attributes, statements
    },
};

#[cfg(not(feature = "attributes"))]
AssignmentStatement: AssignmentStatement = {
    <lhs: LhsExpression> "=" <rhs: ExpressionNode> => AssignmentStatement {
        operator: AssignmentOperator::Equal, lhs, rhs
    },
    <lhs: LhsExpression> <operator: CompoundAssignmentOperator> <rhs: ExpressionNode> => AssignmentStatement {
        operator, lhs, rhs
    },
    <lhs: WithSpan<"_">> "=" <rhs: ExpressionNode> => {
        let (lhs, span) = lhs;
        let lhs = Expression::TypeOrIdentifier(lhs.to_string().into());
        let lhs = Spanned::new(lhs, span);
        AssignmentStatement { operator: AssignmentOperator::Equal, lhs, rhs }
    },
};

CompoundAssignmentOperator: AssignmentOperator = {
    "+=" => AssignmentOperator::PlusEqual,
    "-=" => AssignmentOperator::MinusEqual,
    "*=" => AssignmentOperator::TimesEqual,
    "/=" => AssignmentOperator::DivisionEqual,
    "%=" => AssignmentOperator::ModuloEqual,
    "&=" => AssignmentOperator::AndEqual,
    "|=" => AssignmentOperator::OrEqual,
    "^=" => AssignmentOperator::XorEqual,
    ">>=" => AssignmentOperator::ShiftRightAssign,
    "<<=" => AssignmentOperator::ShiftLeftAssign,
};

#[cfg(not(feature = "attributes"))]
IncrementStatement: IncrementStatement = {
    <expression: Spanned<LhsExpression>> "++" => IncrementStatement {
        expression
    },
};

#[cfg(not(feature = "attributes"))]
DecrementStatement: DecrementStatement = {
    <expression: Spanned<LhsExpression>> "--" => DecrementStatement {
        expression
    },
};

IfStatement: IfStatement = {
    <attributes: Attribute*> <if_clause: IfClause> <else_if_clauses: ElseIfClause*> <else_clause: ElseClause?> => IfStatement {
        attributes, if_clause, else_if_clauses, else_clause
    },
};

IfClause: IfClause = "if" <expression: ExpressionNode> <body: CompoundStatement> => IfClause {
    expression, body
};

#[cfg(not(feature = "attributes"))]
ElseIfClause: ElseIfClause = "else" "if" <expression: ExpressionNode> <body: CompoundStatement> => ElseIfClause {
    expression, body
};

#[cfg(not(feature = "attributes"))]
ElseClause: ElseClause = "else" <body: CompoundStatement> => ElseClause {
    body
};

SwitchStatement: SwitchStatement = {
    <attributes: Attribute*> "switch" <expression: ExpressionNode> <body: SwitchBody> => {
        let (body_attributes, clauses) = body;
        SwitchStatement {
            attributes, expression, body_attributes, clauses
        }
    },
};

SwitchBody: (Vec<Attribute>, Vec<SwitchClause>) = {
    <Attribute*> "{" <SwitchClause+> "}",
};

SwitchClause: SwitchClause = {
    CaseClause,
    DefaultAloneClause,
};

#[cfg(not(feature = "attributes"))]
CaseClause: SwitchClause = {
    "case" <case_selectors: CaseSelectors> ":"? <body: CompoundStatement> => SwitchClause {
        case_selectors, body
    },
};

#[cfg(not(feature = "attributes"))]
DefaultAloneClause: SwitchClause = {
    "default" ":"? <body: CompoundStatement> => SwitchClause {
        case_selectors: vec![CaseSelector::Default], body
    },
};

CaseSelectors: Vec<CaseSelector> = {
    Comma1<CaseSelector>,
};

CaseSelector: CaseSelector = {
    "default" => CaseSelector::Default,
    ExpressionNode => CaseSelector::Expression(<>),
};

LoopStatement: LoopStatement = {
    <attributes: Attribute*> "loop" <body_attributes: Attribute*> "{" <statements: StatementNode*> <continuing: ContinuingStatement?> "}" => {
        let body = CompoundStatement { attributes: body_attributes, statements };
        LoopStatement {
            attributes, body, continuing
        }
    },
};

ForStatement: ForStatement = {
     <attributes: Attribute*> "for" "(" <header: ForHeader> ")" <body: CompoundStatement> => {
        let (initializer, condition, update) = header;
        ForStatement {
            attributes, initializer: initializer.map(Into::into), condition, update: update.map(Into::into), body
        }
    },
};

ForHeader: (Option<StatementNode>, Option<ExpressionNode>, Option<StatementNode>) = {
    <ForInit?> ";" <ExpressionNode?> ";" <ForUpdate?>,
};

ForInit: StatementNode = {
    <statement: WithSpan<VariableOrValueStatement>>  => {
        let (statement, span) = statement;
        Spanned::new(Statement::Declaration(statement), span)
    },
    <statement: WithSpan<VariableUpdatingStatement>> => {
        let (statement, span) = statement;
        Spanned::new(statement, span)
    },
    <statement: WithSpan<FuncCallStatement>> => {
        let (statement, span) = statement;
        Spanned::new(Statement::FunctionCall(statement), span)
    },
};

ForUpdate: StatementNode = {
    <statement: WithSpan<VariableUpdatingStatement>> => {
        let (statement, span) = statement;
        Spanned::new(statement, span)
    },
    <statement: WithSpan<FuncCallStatement>> => {
        let (statement, span) = statement;
        Spanned::new(Statement::FunctionCall(statement), span)
    },
};

WhileStatement: WhileStatement = {
    <attributes: Attribute*> "while" <condition: ExpressionNode> <body: CompoundStatement> => WhileStatement {
        attributes, condition, body
    },
};

#[cfg(not(feature = "attributes"))]
BreakStatement: BreakStatement = {
    "break" => BreakStatement {},
};

#[cfg(not(feature = "attributes"))]
BreakIfStatement: BreakIfStatement = {
    "break" "if" <expression: ExpressionNode> ";" => BreakIfStatement {
        expression
    },
};

#[cfg(not(feature = "attributes"))]
ContinueStatement: ContinueStatement = {
    "continue" => ContinueStatement {},
};

#[cfg(not(feature = "attributes"))]
ContinuingStatement = "continuing" <ContinuingCompoundStatement>;

#[cfg(not(feature = "attributes"))]
ContinuingCompoundStatement: ContinuingStatement = {
    <attributes: Attribute*> "{" <statements: StatementNode*> <break_if: BreakIfStatement?> "}" => {
        let body = CompoundStatement { attributes, statements };
        ContinuingStatement { body, break_if }
    },
};

#[cfg(not(feature = "attributes"))]
ReturnStatement: ReturnStatement = {
    "return" <expression: ExpressionNode?> => ReturnStatement {
        expression
    },
};

#[cfg(not(feature = "attributes"))]
DiscardStatement: DiscardStatement = {
    "discard" => DiscardStatement {},
};

#[cfg(not(feature = "attributes"))]
FuncCallStatement: FunctionCallStatement = <call: CallPhrase> => FunctionCallStatement {
    call
};

#[cfg(not(feature = "attributes"))]
ConstAssertStatement: ConstAssertStatement = {
    "const_assert" <expression: ExpressionNode> => ConstAssertStatement {
        expression
    },
};

pub Statement: Statement = {
    ";" => Statement::Void,
    <ReturnStatement> ";" => Statement::Return(<>),
    <IfStatement> => Statement::If(<>),
    <SwitchStatement> => Statement::Switch(<>),
    <LoopStatement> => Statement::Loop(<>),
    <ForStatement> => Statement::For(<>),
    <WhileStatement> => Statement::While(<>),
    <FuncCallStatement> ";" => Statement::FunctionCall(<>),
    <VariableOrValueStatement> ";" => Statement::Declaration(<>),
    <BreakStatement> ";" => Statement::Break(<>),
    <ContinueStatement> ";" => Statement::Continue(<>),
    <DiscardStatement> ";" => Statement::Discard(<>),
    <VariableUpdatingStatement> ";" => <>,
    <CompoundStatement> => Statement::Compound(<>),
    <ConstAssertStatement> ";" => Statement::ConstAssert(<>),
};

StatementNode: StatementNode = Spanned<Statement>;

VariableUpdatingStatement: Statement = {
    AssignmentStatement => Statement::Assignment(<>),
    IncrementStatement => Statement::Increment(<>),
    DecrementStatement => Statement::Decrement(<>),
};

// 10. FUNCTIONS
// https://www.w3.org/TR/WGSL/#functions

FunctionDecl: Function = {
    <attributes: Attribute*> <header: FunctionHeader> <body: CompoundStatement> => {
        let (ident, parameters, return_attributes, return_type) = header;
        Function { attributes, ident, parameters, return_attributes, return_type, body }
    },
};

FunctionHeader: (Ident, Vec<FormalParameter>, Vec<Attribute>, Option<TypeExpression>) = {
    "fn" <ident: Ident> "(" <parameters: ParamList?> ")" <ret: ("->" <Attribute*> <TemplateElaboratedIdent>)?> => {
        let (return_attributes, return_type) = ret.map(|(return_attributes, return_type)| {
            (return_attributes, Some(return_type))
        }).unwrap_or_default();
        let parameters = parameters.unwrap_or_default();
        (ident, parameters, return_attributes, return_type)
    },
};

ParamList: Vec<FormalParameter> = {
    Comma1<Param>,
};

Param: FormalParameter = {
    <attributes: Attribute*> <ident: Ident> ":" <ty: TypeSpecifier> => FormalParameter {
        attributes, ident, ty
    },
};

// 11. ATTRIBUTES
// https://www.w3.org/TR/WGSL/#attributes

Attribute: Attribute = {
    <l: @L> "@" <name: IdentPatternToken> <arguments: ArgumentExpressionList?> <r: @R> =>? {
        parse_attribute(name, arguments)
            .map_err(|e| lalrpop_util::ParseError::User{ error: (l, e, r) })
    },
    <l: @L> "@" <name: Keyword> <arguments: ArgumentExpressionList?> <r: @R> =>? {
        parse_attribute(name, arguments)
            .map_err(|e| lalrpop_util::ParseError::User{ error: (l, e, r) })
    },
};

DiagnosticControl: (DiagnosticSeverity, String) = {
    "(" <SeverityControlName> "," <DiagnosticRuleName> ","? ")",
};

// EXTENSION: WESL-IMPORTS
// https://github.com/wgsl-tooling-wg/wesl-spec/blob/imports-update/Imports.md
// date: 2025-01-18, hash: 2db8e7f681087db6bdcd4a254963deb5c0159775

#[cfg(feature = "imports")]
pub TranslationUnit: TranslationUnit = {
    <imports: Import*> <global_directives: GlobalDirective*> <global_declarations: GlobalDecl*> => TranslationUnit {
        imports, global_directives, global_declarations
    },
};

#[cfg(all(feature = "imports", not(feature = "attributes")))]
pub Import: Import = {
    "import" <relative: ImportRelative?> <mut import: ImportPath> ";" => match relative {
        Some(mut relative) => {
            relative.extend(import.path);
            import.path = relative;
            import
        },
        None => import,
    }
};

#[cfg(all(feature = "imports", feature = "attributes"))]
pub Import: Import = {
    <attributes: Attribute*> "import" <relative: ImportRelative?> <mut import: ImportPath> ";" => {
        match relative {
            Some(mut relative) => {
                relative.extend(&import.path);
                import.path = relative;
            },
            None => {
                let mut absolute = PathBuf::from("/");
                absolute.push(import.path);
                import.path = absolute;
            },
        };
        import.attributes = attributes;
        import
    }
};

#[cfg(feature = "imports")]
ImportRelative: std::path::PathBuf = {
    "self" "::" <parents: ("super" "::")*> => {
        PathBuf::from_iter(std::iter::once(".").chain(parents.iter().map(|_| "..")))
    },
    "super" "::" <parents: ("super" "::")*> => {
        PathBuf::from_iter(std::iter::once("..").chain(parents.iter().map(|_| "..")))
    },
    "crate" "::" => {
        PathBuf::from("")
    },
};

#[cfg(feature = "imports")]
ImportPath: Import = {
    <path: (<Ident> "::")+> <content: ImportContent> => Import {
        #[cfg(feature = "attributes")] attributes: Vec::new(),
        path: std::path::PathBuf::from_iter(path.iter().map(|ident| ident.name().to_string())), content
    },
    ItemImport => Import {
        #[cfg(feature = "attributes")] attributes: Vec::new(),
        path: Default::default(), content: ImportContent::Item(<>)
    },
};

#[cfg(feature = "imports")]
ImportContent: ImportContent = {
    CollectionImport => ImportContent::Collection(<>),
    ItemImport => ImportContent::Item(<>),
};

#[cfg(feature = "imports")]
ItemImport: ImportItem = {
    <ident: Ident> <rename: ("as" <Ident>)?> => ImportItem {
        ident, rename
    }
};

#[cfg(feature = "imports")]
CollectionImport: Vec<Import> = {
    "{" <Comma1<ImportPath>> "}"
};

// EXTENSION: WESL-CONDITIONAL-TRANSLATION
// https://github.com/wgsl-tooling-wg/wesl-spec/pull/19
// date: 2024-09-14, hash: b1446356ff206e5826a58812895bc6d3d64a2220

#[cfg(feature = "imports")]
Keyword: String = {
    "alias" => <>.to_string(),
    "break" => <>.to_string(),
    "case" => <>.to_string(),
    "const" => <>.to_string(),
    "const_assert" => <>.to_string(),
    "continue" => <>.to_string(),
    "continuing" => <>.to_string(),
    "default" => <>.to_string(),
    "diagnostic" => <>.to_string(),
    "discard" => <>.to_string(),
    "else" => <>.to_string(),
    "enable" => <>.to_string(),
    "false" => <>.to_string(),
    "fn" => <>.to_string(),
    "for" => <>.to_string(),
    "if" => <>.to_string(),
    "let" => <>.to_string(),
    "loop" => <>.to_string(),
    "override" => <>.to_string(),
    "requires" => <>.to_string(),
    "return" => <>.to_string(),
    "struct" => <>.to_string(),
    "switch" => <>.to_string(),
    "true" => <>.to_string(),
    "var" => <>.to_string(),
    "while" => <>.to_string(),
    "as" => <>.to_string(),
    "import" => <>.to_string(),
};

#[cfg(feature = "attributes")]
EnableDirective: EnableDirective = {
    <attributes: Attribute*> "enable" <extensions: EnableExtensionList> ";" => EnableDirective {
        attributes, extensions
    },
};

#[cfg(feature = "attributes")]
RequiresDirective: RequiresDirective = {
    <attributes: Attribute*> "requires" <extensions: SoftwareExtensionList> ";" => RequiresDirective {
        attributes, extensions
    },
};

#[cfg(feature = "attributes")]
DiagnosticDirective: DiagnosticDirective = {
    <Attribute*> "diagnostic" <DiagnosticControl> ";" => {
        let (attributes, (severity, rule_name)) = (<>);
        DiagnosticDirective { attributes, severity, rule_name }
    },
};

#[cfg(feature = "attributes")]
StructDecl: Struct = {
    <attributes: Attribute*> "struct" <ident: Ident> <members: StructBodyDecl> => Struct {
        attributes, ident, members
    },
};

#[cfg(feature = "attributes")]
TypeAliasDecl: TypeAlias = {
    <attributes: Attribute*> "alias" <ident: Ident> "=" <ty: TypeSpecifier> => TypeAlias {
        attributes, ident, ty
    },
};

#[cfg(feature = "attributes")]
VariableOrValueStatement: Declaration = {
    VariableDecl,
    <mut decl: VariableDecl> "=" <initializer: ExpressionNode> => {
        decl.initializer = Some(initializer);
        decl
    },
    <attributes: Attribute*> "let" <id_ty: OptionallyTypedIdent> "=" <initializer: ExpressionNode> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes,
            kind: DeclarationKind::Let,
            ident,
            ty,
            initializer: Some(initializer),
        }
    },
    <attributes: Attribute*> "const" <id_ty: OptionallyTypedIdent> "=" <initializer: ExpressionNode> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes,
            kind: DeclarationKind::Const,
            ident,
            ty,
            initializer: Some(initializer),
        }
    },
};

#[cfg(feature = "attributes")]
VariableDecl: Declaration = {
    <attributes: Attribute*> <l: @L> "var" <template_args: TemplateList?> <r: @R> <id_ty: OptionallyTypedIdent> =>? {
        let (ident, ty) = id_ty;
        let address_space = parse_var_template(template_args)
            .map_err(|e| lalrpop_util::ParseError::User{ error: (l, e, r) })?;
        Ok(Declaration {
            attributes,
            kind: DeclarationKind::Var(address_space),
            ident,
            ty,
            initializer: None,
        })
    },
};

#[cfg(feature = "attributes")]
GlobalVariableDecl: Declaration = {
    <mut decl: VariableDecl> <initializer: ("=" <ExpressionNode>)?> => {
        decl.initializer = initializer.map(Into::into);
        decl
    },
};

#[cfg(feature = "attributes")]
GlobalValueDecl: Declaration = {
    <attributes: Attribute*> "const" <id_ty: OptionallyTypedIdent> "=" <initializer: ExpressionNode> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes,
            kind: DeclarationKind::Const,
            ident,
            ty,
            initializer: Some(initializer),
        }
    },
    <attributes: Attribute*> "override" <id_ty: OptionallyTypedIdent> <initializer: ("=" <ExpressionNode>)?> => {
        let (ident, ty) = id_ty;
        Declaration {
            attributes,
            kind: DeclarationKind::Override,
            ident,
            ty,
            initializer: initializer.map(Into::into),
        }
    },
};

#[cfg(feature = "attributes")]
AssignmentStatement: AssignmentStatement = {
    <lhs: Spanned<LhsExpression>> "=" <rhs: ExpressionNode> => AssignmentStatement {
        attributes: vec![], operator: AssignmentOperator::Equal, lhs, rhs
    },
    <lhs: Spanned<LhsExpression>> <operator: CompoundAssignmentOperator> <rhs: ExpressionNode> => AssignmentStatement {
        attributes: vec![], operator, lhs, rhs
    },
    <l: @L> "_" <r: @R> "=" <rhs: ExpressionNode> => {
        let lhs = Expression::TypeOrIdentifier(Ident::new("_".to_string()).into());
        let span = (l..r).into();
        let lhs = Spanned::new(lhs, span);
        AssignmentStatement { attributes: vec![], operator: AssignmentOperator::Equal, lhs, rhs }
    },
};

#[cfg(feature = "attributes")]
IncrementStatement: IncrementStatement = {
    <expression: Spanned<LhsExpression>> "++" => IncrementStatement {
        attributes: vec![], expression
    },
};

#[cfg(feature = "attributes")]
DecrementStatement: DecrementStatement = {
    <expression: Spanned<LhsExpression>> "--" => DecrementStatement {
        attributes: vec![], expression
    },
};

#[cfg(feature = "attributes")]
ElseIfClause: ElseIfClause = "else" "if" <expression: ExpressionNode> <body: CompoundStatement> => ElseIfClause {
    attributes: vec![], expression, body
};

#[cfg(feature = "attributes")]
ElseClause: ElseClause = "else" <body: CompoundStatement> => ElseClause {
    attributes: vec![], body
};

#[cfg(feature = "attributes")]
CaseClause: SwitchClause = {
    <attributes: Attribute*> "case" <case_selectors: CaseSelectors> ":"? <body: CompoundStatement> => SwitchClause {
        attributes, case_selectors, body
    },
};

#[cfg(feature = "attributes")]
DefaultAloneClause: SwitchClause = {
    <attributes: Attribute*> "default" ":"? <body: CompoundStatement> => SwitchClause {
        attributes, case_selectors: vec![CaseSelector::Default], body
    },
};

#[cfg(feature = "attributes")]
BreakStatement: BreakStatement = {
    <attributes: Attribute*> "break" => BreakStatement {
        attributes
    },
};

#[cfg(feature = "attributes")]
BreakIfStatement: BreakIfStatement = {
    <attributes: Attribute*> "break" "if" <expression: ExpressionNode> ";" => BreakIfStatement {
        attributes, expression
    },
};

#[cfg(feature = "attributes")]
ContinueStatement: ContinueStatement = {
    <attributes: Attribute*> "continue" => ContinueStatement {
        attributes
    },
};

#[cfg(feature = "attributes")]
ContinuingStatement: ContinuingStatement = <Attribute*> "continuing" <ContinuingCompoundStatement> => {
    let (attributes, mut statement) = (<>);
    statement.attributes = attributes;
    statement
};

#[cfg(feature = "attributes")]
ContinuingCompoundStatement: ContinuingStatement = {
    <attributes: Attribute*> "{" <statements: StatementNode*> <break_if: BreakIfStatement?> "}" => {
        let body = CompoundStatement { attributes, statements };
        ContinuingStatement { attributes: Vec::new(), body, break_if }
    },
};

#[cfg(feature = "attributes")]
ReturnStatement: ReturnStatement = {
    <attributes: Attribute*> "return" <expression: ExpressionNode?> => ReturnStatement {
        attributes, expression
    },
};

#[cfg(feature = "attributes")]
DiscardStatement: DiscardStatement = {
    <attributes: Attribute*> "discard" => DiscardStatement {
        attributes
    },
};

#[cfg(feature = "attributes")]
FuncCallStatement: FunctionCallStatement = <attributes: Attribute*> <call: CallPhrase> => FunctionCallStatement {
    attributes, call
};

#[cfg(feature = "attributes")]
ConstAssertStatement: ConstAssertStatement = {
    <attributes: Attribute*> "const_assert" <expression: ExpressionNode> => ConstAssertStatement {
        attributes, expression
    },
};

// ===================
// === End grammar ===
// ===================

// ----------- below: macros -----------

Comma<T>: Vec<T> = {
    <mut v: (<T> ",")*> <e: T?> => match e {
        Some(e) => {
            v.push(e);
            v
        }
        None => v,
    }
};

Spanned<T>: Spanned<T> = <@L> <T> <@R> => {
    let (l, t, r) = (<>);
    let span = (l..r).into();
    Spanned::new(t, span)
};

WithSpan<T>: (T, Span) = <@L> <T> <@R> => {
    let (l, t, r) = (<>);
    let span = (l..r).into();
    (t, span)
};

Comma1<T> = Sep1<",", T>;

Sep1<S, T>: Vec<T> = {
    <mut v: (<T> S)*> <e: T> S? => {
        v.push(e);
        v
    }
};
